// ----------------------------------
// RSDK Project: Sonic 1/Sonic 2
// Script Description: V Platform 2 Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

//-------Aliases-------//
private alias 50 : TYPE_VPLATFORM2

// Function declarations
reserve function VPlatform2_DebugDraw
reserve function VPlatform2_DebugSpawn

// Static Values

// Tables

function VPlatform2_DebugDraw
	DrawSprite(0)
end function


function VPlatform2_DebugSpawn
	CreateTempObject(TypeName[V Platform 2], 0, object.xpos, object.ypos)
	object[tempObjectPos].value0 = object.ypos
	object[tempObjectPos].propertyValue = object.direction
end function


event ObjectMain
	temp7 = object.ypos
	GetTableValue(temp0, 12, StageSetup_table9)
	if object.propertyValue == 1
		FlipSign(temp0)
		temp0 += 0x4000
	else
		temp0 -= 0x4000
	end if
	temp0 <<= 7
	object.ypos = object.value0
	object.ypos -= temp0
	temp1 = object.ypos
	temp7 &= -0x10000
	object.value1 = object.ypos
	object.value1 &= -0x10000
	object.value1 -= temp7
	object.ypos = temp7
	foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
		BoxCollisionTest(C_BOX, object.entityPos, -32, -2, 32, 138, currentPlayer, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO)
		BoxCollisionTest(C_PLATFORM, object.entityPos, -32, -2, 32, 12, currentPlayer, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO)
		if checkResult == 1
			object[currentPlayer].ypos += object.value1
		end if
	next
	object.ypos = temp1
end event


event ObjectDraw
	DrawSprite(0)
end event


event ObjectStartup
	LoadSpriteSheet("GHZ/Objects.gif")
	SpriteFrame(-32, -8, 64, 144, 191, 1)
	foreach (TypeName[V Platform 2], arrayPos0, ALL_ENTITIES)
		object[arrayPos0].value0 = object[arrayPos0].ypos
	next
	SetTableValue(TypeName[V Platform 2], DebugMode_ObjCount, DebugMode_TypesTable)
	SetTableValue(VPlatform2_DebugDraw, DebugMode_ObjCount, DebugMode_DrawTable)
	SetTableValue(VPlatform2_DebugSpawn, DebugMode_ObjCount, DebugMode_SpawnTable)
	DebugMode_ObjCount++
end event

event RSDKDraw
	DrawSprite(0)
	
	if editor.showGizmos == true
		// Clarify that this object isn't just part of the level terrain, draw some lines
		
		// TODO: probably best to make sure these arrows somewhat accurately represent how far it'll go in-game
		// i have no idea how these currently look since i haven't been given any fancy re2 betas yet but the values probably look wrong
		// the "correct" values could probably be reused from Mania's version of the object preview, although i don't have access to that either...
		// edit: the miracle happen;
		// github down rn but snag values from there once it's back up
		// perhaps used cropped frame so that bottom of sprite so lower end doesn't stick out more than it should, only have grass ghost
		
		editor.drawingOverlay = true

		temp0 = 0x204000
		temp0 += object.ypos
		temp1 = -0x1FC000
		temp1 += object.ypos

		DrawLine(object.xpos, temp0, object.xpos, temp1, 255, 255, 255)

		editor.drawingOverlay = false
	end if
end event

event RSDKLoad
	LoadSpriteSheet("GHZ/Objects.gif")
	SpriteFrame(-32, -8, 64, 144, 191, 1)

	AddEditorVariable("startDir")
	SetActiveVariable("startDir")
	AddEnumVariable("Up", 0)
	AddEnumVariable("Down", 1)
end event
